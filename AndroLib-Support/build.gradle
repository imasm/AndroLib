apply plugin: 'android-library'
apply plugin: 'maven'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode rootProject.versionCode
        versionName rootProject.versionName
    }
}

dependencies {
    compile 'com.android.support:support-v4:+'
}

version rootProject.publishVersion
group rootProject.publishGroup


// Install - upload aar library to local maven repository
// ---------------------------------------------------------------------------------
uploadArchives {
    repositories {
        mavenDeployer {
            repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
        }
    }
}

task install(dependsOn: uploadArchives)


// makeJar - Create jar file in builds/libs
// ---------------------------------------------------------------------------------
// This is the actual solution, as in http://stackoverflow.com/a/19037807/1002054
task clearJar(type: Delete) {
    delete 'build/libs/' + project.name + '-' + rootProject.versionName + '.jar'
}

task makeJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename ('classes.jar', project.name + '-' + rootProject.versionName + '.jar')
}

makeJar.dependsOn(clearJar, build)