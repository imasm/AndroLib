apply plugin: 'android-library'
apply plugin: 'maven'


android {
    compileSdkVersion 18
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 16

        versionCode 1
        versionName "1.0"
    }
}


archivesBaseName = 'androlib'
version "1.0"
group "net.compactsys"


// Install - upload aar library to local maven repository
// ---------------------------------------------------------------------------------
uploadArchives {
    repositories {
        mavenDeployer {
            repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
        }
    }
}

task install(dependsOn: uploadArchives)


// makeJar - Create jar file in builds/libs
// ---------------------------------------------------------------------------------
// This is the actual solution, as in http://stackoverflow.com/a/19037807/1002054
task clearJar(type: Delete) {
    delete 'build/libs/' + project.name + '-' + project.version + '.jar'
}

task makeJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename ('classes.jar', project.name + '-' + project.version + '.jar')
}

makeJar.dependsOn(clearJar, build)